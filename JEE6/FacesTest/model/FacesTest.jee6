application "JEE6 Test Application" context "/faces" package org.jeegen.faces timeout 2 integration;
persistence unit "facesDS" jndi "jdbc/facesDS";
persistence unit "authDS"  jndi "jdbc/authDS";

security domain "jeegen";
smtp "mail/Default";
locale "de" default;
locale "en";

options AddressOption
{
	"address.work",
	"address.home"
}

entity Address
{
	Int                  position;
	Text                 street;
	Text                 plz;
	Text                 location;
	Boolean              active;
	Option AddressOption addressOption;
}

options UserInfo editable
{
	Text id              login;
	Entity Address []    addresses;
	Text                 name;
	Email                mail;
	Date                 birth;
	Timestamp update     changed;
	Blob                 image;
}

entity HistoryEntry
{
	Timestamp auto         creation;
	Text                   comment;
}

entity EntityEntry
{
	Timestamp auto         creation;
	Text                   comment;
	Option AddressOption   addressOption;
}

entity Startup cloneable
{
	Timestamp auto         creation;
	Timestamp update       changed;
	Timestamp              timestamp1;
	Timestamp              timestamp2;
	Date                   dateElement;

	History HistoryEntry   history;
	Entity  EntityEntry [] entities;
}

options Preferences editable
{
	Text entry;
}

entity AuthInfo
{
	Email id                email;
	Text                    firstName;
	Text                    lastName; 
	Text                    password;
	Text                    passwordUncoded transient;
	Boolean                 active;
	Timestamp auto          created;
	Entity SiteAuthInfo []  sites;
} persistence unit "authDS"

options SiteInfo editable
{
	Text id                 skuPrefix;
	Text                    name;
} persistence unit "authDS"

entity SiteAuthInfo
{
	Option         SiteInfo site;
	Timestamp auto          created;
	Date                    expire;
	Boolean                 active;
} persistence unit "authDS"

entity Lock
{
	Text id                 code;
	Int                     value;
	Text                    info transient;
}

process Admin roles "admin"
{
	UserInfo
}

process Info
{
	Integer "build" 	ref "java:global/build";
	Boolean default false "productive" ref "java:global/productive";

	Startup
}
