application "Test" context "/purchasing" package de.itemis.purchasing;
persistence unit "purchasingDS" jndi "jdbc/purchasingDS";
security domain "itemis";
smtp "mail/Default";
locale "de" default;
locale "en";

options UserInfo editable
{
	Text id           login;
	Text              mail;
	Timestamp update  changed;
	Text              forename;
	Text              surename;
	Boolean           active;
	Boolean           authorizer;
	Text              name transient;
};

entity Mail
{
	Timestamp created;
	Timestamp sent;
	Text      subject;
	Text      sender;
	Text      receiver;
	Text      cc;
	Clob      message;
};

options Vendor editable
{
	Text    name;
	Boolean active;
};

options Distributor editable
{
	Text    name;
	Boolean active;
	Text    contact;
	Text    phone;
	Text    mail;
};

options InventoryType editable
{
	Text id type;
	Text    label;
	Entity  Vendor vendor;
	Boolean active;
};

entity OrderPosition filterable cloneable
{
	Option InventoryType inventoryType;
	Int                  amount;
	Text                 model;
	Text                 comment;
	Timestamp            received;
	Boolean              canceled;
	Entity UserInfo      owner;
	Entity UserInfo      authorizer;
	Text                 position transient;
};

entity Purchase
{
	Timestamp auto          created;
	Text                    article;
	Entity OrderPosition [] positions;
	Option Distributor      distributor;
	Entity UserInfo         buyer;
};

options InventoryState
{
	"UNKNOWN",
	"ORDERED",
	"RECEIVED",
	"IN_USE",
	"REPAIR",
	"NOT_IN_USE",
	"MARKED_FOR_SELL",
	"SOLD",
	"GIVE_AWAY",
	"DEFECT",
	"THEFT"
};

entity InventoryHistoryEntry cloneable
{
	Timestamp             created;
	Option InventoryState state;
	Entity UserInfo       owner;
	Entity UserInfo       commentator;
	Text                  utilization;
	Text                  comment;
	Number                sell;
};

entity Inventory filterable
{
	History InventoryHistoryEntry history; 
	Text                          serialNumber;
	Option InventoryType          inventoryType;
	Boolean                       infrastructure;
};

process Inventory roles "controlling","it_support","vorstand"
{
	Inventory, InventoryHistoryEntry
};

process Purchasing roles "controlling","it_support","vorstand"
{
	Purchase, OrderPosition
};

process Admin roles "admin"
{
	Vendor, Distributor, InventoryType
};

process User roles "itemis"
{
	UserInfo
};
